// Oxidrive Query Language

WHITESPACE = _{ " " }

reserved_char = _ { "*" | ":" | "(" | ")" | "\"" }

char = _{ !reserved_char ~ (LETTER | EMOJI | SYMBOL | PUNCTUATION) }

mod = _ { not }
    not = { "-" }

op = _{ and | or }
	and = { "AND" }
    or = { "OR" }

all = ${ "*" }

match = ${ "*" }
text = ${ (char | ":")+ }

key = @{ !op ~ char+ }
value = @{ !op ~ (match | text)+ }

quoted_value = @{ "\"" ~ value ~ (WHITESPACE? ~ value)* ~ "\"" }

tag = ${ mod? ~ (key ~ ":" ~ value | (key ~ ":" ~ quoted_value) | key) }

tags = { tag+ }

term = _{ tags | "(" ~ filter ~ ")" }

filter = { term ~ (op ~ term)* }

query = _{ SOI ~ (all | filter) ~ EOI }